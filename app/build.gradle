plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = rootProject.file("app/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.example.asteroid"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        productionRelease {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    flavorDimensions "version"

    productFlavors {
        def label = "Asteroid"
        def flavorProductionSigningConfig = signingConfigs.productionRelease

        Qa {
            dimension "version"
            applicationIdSuffix ".qa"

            buildConfigField('String', 'BASE_URL', localProperties['URL_BASE_QA'])
            buildConfigField('String', 'AMBIENTE', localProperties['AMBIENTE_QA'])

            manifestPlaceholders = [label: label]

            signingConfig flavorProductionSigningConfig
        }

        Stage {
            dimension "version"
            applicationIdSuffix ".stage"

            buildConfigField('String', 'BASE_URL', localProperties['URL_BASE_STAGE'])
            buildConfigField('String', 'AMBIENTE', localProperties['AMBIENTE_STAGE'])

            manifestPlaceholders = [label: label+"-Stage"]

            signingConfig flavorProductionSigningConfig
        }

        PreProd {
            dimension "version"
            applicationIdSuffix ".hml"

            buildConfigField('String', 'BASE_URL', localProperties['URL_BASE_PREPROD'])
            buildConfigField('String', 'AMBIENTE', localProperties['AMBIENTE_PREPROD'])

            manifestPlaceholders = [label: label+"-PREPROD"]

            signingConfig flavorProductionSigningConfig
        }

        Prod {
            dimension "version"
            applicationIdSuffix ".Prod"

            buildConfigField('String', 'BASE_URL', localProperties['URL_BASE_PROD'])
            buildConfigField('String', 'AMBIENTE', localProperties['AMBIENTE_PROD'])

            manifestPlaceholders = [label: label+"-Prod"]

            signingConfig flavorProductionSigningConfig
        }
    }

    buildTypes {
        release {
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}